       PROCESS ARITH(EXTEND)
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PBCTUF64.
      *****************************************************************
      * OVERFIEW                                                      *
      * --------                                                      *
      * TEST UNSIGNED INTEGER 64 BITS <> FIXED INTEGER                *
      *****************************************************************

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       DATA DIVISION.
      *****************************************************************
      *        W O R K I N G    S T O R A G E    S E C T I O N        *
      *****************************************************************
       WORKING-STORAGE SECTION.
       01  W-PB-FIXED64                       PIC X(8).
       01  W-ZOS-UINT64                       PIC 9(18) COMP-5.
       01  W-BYTES-USED                       PIC 9(8) COMP-5.

       01  W-EXPECTED-PB-FIXED64              PIC X(8).
       01  W-EXPECTED-ZOS-UINT64              PIC 9(18) COMP-5.

       01  W-PB-FIXED64-ARRAY-LEN             PIC 9(4) COMP-5 VALUE 8.
       01  W-PB-FIXED64-HEX-STRING            PIC X(16).

       01  W-RETURN-CODE                      PIC 9(4) COMP VALUE 0.
      *****************************************************************
      *    P R O C E D U R E  D I V I S I O N   S E C T I O N         *
      *****************************************************************
       PROCEDURE DIVISION.

      * ---------------------------------------------------------------
      * TEST TO ZOS Unsigned Int 64
      * ---------------------------------------------------------------
           DISPLAY "TO-ZOSUINT64 ======= 0 =============="
           MOVE X'0000000000000000' TO W-PB-FIXED64.
           MOVE 0 to W-EXPECTED-ZOS-UINT64.
           PERFORM TEST-PBFI2U64 THRU END-TEST-PBFI2U64.
           
           DISPLAY "TO-ZOSUINT64 ======= 1 =============="
           MOVE X'0100000000000000' TO W-PB-FIXED64.
           MOVE 1 to W-EXPECTED-ZOS-UINT64.
           PERFORM TEST-PBFI2U64 THRU END-TEST-PBFI2U64.

           DISPLAY "TO-ZOSUINT64 ======= 300 =============="
           MOVE X'2C01000000000000' TO W-PB-FIXED64.
           MOVE 300 to W-EXPECTED-ZOS-UINT64.
           PERFORM TEST-PBFI2U64 THRU END-TEST-PBFI2U64.

           DISPLAY "TO-ZOSUINT64 ======= 45483 =============="
           MOVE X'ABB1000000000000' TO W-PB-FIXED64.
           MOVE 45483 to W-EXPECTED-ZOS-UINT64.
           PERFORM TEST-PBFI2U64 THRU END-TEST-PBFI2U64.

           DISPLAY "TO-ZOSUINT64 == 9223372036854775807 ====="
           MOVE X'FFFFFFFFFFFFFF7F' TO W-PB-FIXED64.
           MOVE 9223372036854775807 to W-EXPECTED-ZOS-UINT64.
           PERFORM TEST-PBFI2U64 THRU END-TEST-PBFI2U64.

      * ---------------------------------------------------------------
      * TEST TO PROTOBUF Unsigned Int 64
      * ---------------------------------------------------------------
           DISPLAY "TO-PB-FIXED64 ========== 0 =========="
           COMPUTE W-ZOS-UINT64 = 0.
           MOVE X'0000000000000000' to W-EXPECTED-PB-FIXED64.
           PERFORM TEST-PBU642FI THRU END-TEST-PBU642FI.

           DISPLAY "TO-PB-FIXED64 ========== 1 =========="
           COMPUTE W-ZOS-UINT64 = 1.
           MOVE X'0100000000000000' to W-EXPECTED-PB-FIXED64.
           PERFORM TEST-PBU642FI THRU END-TEST-PBU642FI.

           DISPLAY "TO-PB-FIXED64 ========== 300 =========="
           COMPUTE W-ZOS-UINT64 = 300.
           MOVE X'2C01000000000000' to W-EXPECTED-PB-FIXED64.
           PERFORM TEST-PBU642FI THRU END-TEST-PBU642FI.

           DISPLAY "TO-PB-FIXED64 ========== 45483 =========="
           COMPUTE W-ZOS-UINT64 = 45483.
           MOVE X'ABB1000000000000' to W-EXPECTED-PB-FIXED64.
           PERFORM TEST-PBU642FI THRU END-TEST-PBU642FI.

           DISPLAY "TO-PB-FIXED64 == 9223372036854775807 ====="
           COMPUTE W-ZOS-UINT64 = 9223372036854775807.
           MOVE X'FFFFFFFFFFFFFF7F' to W-EXPECTED-PB-FIXED64.
           PERFORM TEST-PBU642FI THRU END-TEST-PBU642FI.

           MOVE W-RETURN-CODE to RETURN-CODE.
           GOBACK.

       TEST-PBFI2U64.
           CALL 'PBFI2U64' USING W-PB-FIXED64 W-ZOS-UINT64 W-BYTES-USED.
           IF W-ZOS-UINT64 NOT = W-EXPECTED-ZOS-UINT64
              DISPLAY 'ERROR: ZOS-UINT64'
              DISPLAY '    RESULT  : ' W-ZOS-UINT64
              DISPLAY '    EXPECTED: ' W-EXPECTED-ZOS-UINT64
              MOVE 8 TO W-RETURN-CODE
           ELSE
              DISPLAY 'OK'
           END-IF.
       END-TEST-PBFI2U64. EXIT.

       TEST-PBU642FI.
           MOVE LOW-VALUES TO W-PB-FIXED64.
           CALL 'PBU642FI' USING W-ZOS-UINT64 W-PB-FIXED64 W-BYTES-USED.
           IF W-PB-FIXED64 NOT = W-EXPECTED-PB-FIXED64
              DISPLAY 'ERROR: PB-FIXED64'
              CALL 'PBBA2HEX' USING W-PB-FIXED64-ARRAY-LEN
                              W-PB-FIXED64
                              W-PB-FIXED64-HEX-STRING
              DISPLAY '    RESULT  : ' W-PB-FIXED64-HEX-STRING
              CALL 'PBBA2HEX' USING W-PB-FIXED64-ARRAY-LEN
                              W-EXPECTED-PB-FIXED64
                              W-PB-FIXED64-HEX-STRING
              DISPLAY '    EXPECTED: ' W-PB-FIXED64-HEX-STRING
              MOVE 8 TO W-RETURN-CODE
           ELSE
              DISPLAY 'OK'
           END-IF.
       END-TEST-PBU642FI. EXIT.

       END PROGRAM PBCTUF64.

